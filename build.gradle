//////////////////////////////////////////////////////////////////////////////
// This file is the base build-script for the project. There are additional
// config files in the 'config'-directory, that handle different aspects of
// the build and deployment for the project.
//////////////////////////////////////////////////////////////////////////////

apply plugin: 'java-base'

// Please make sure you read the README.md file before you try to understand
// what is going on.

allprojects {
	// You can use 'gradle eclipse' to generate eclipse project files.
	// You can use 'gradle -t' to see a list of availible tasks.
	apply plugin: 'eclipse'

	defaultTasks 'build'

	// Usually you will want to add proxy repositories to nexus instead on
	// adding explicitly here.
	repositories {
		mavenLocal()
		mavenRepo name: 'trifork-releases', urls: 'http://nexus.ci81.trifork.com/content/groups/public/'
		mavenRepo name: 'trifork-snapshots', urls: 'http://nexus.ci81.trifork.com/content/groups/public-snapshots/'
	}
}

subprojects { subproject ->
	
	// Subproject currently always default to java. This might change in the future.
	// If you are making WARs take a look at the 'war' plugin.
	apply plugin: 'java'
	apply plugin: 'deployment'

	targetCompatibility = '1.6'
	sourceCompatibility = '1.6'
	
	// Include references to common dependencies so you can keep
	// versions consistent accross your sub-projects.
	
	apply from: 'config/libraries.groovy'

	// Checkstyle source coding standards.
	// TODO: These setting are a bit verbose, but
	// the implementation seems to be a bit broken (in Gradle 0.9.2).
	apply plugin: 'code-quality'
	checkstyleConfigDir = "$rootDir/config/checkstyle"
	checkstyleConfigFileName = new File(checkstyleConfigDir, "checkstyle.xml")
	checkstyleProperties.checkstyleConfigDir = checkstyleConfigDir

	// Shared dependencies for all subprojects.
	// Logging is a common shared dependency.
	dependencies {
		testCompile	libraries.testng
	}

	// Add some extra memory for testing, this is usually useful.
	test {
		maxHeapSize = "1024m"
	}
}

dependsOnChildren()

// The following is section is adds the plugins from
// https://github.com/trifork/gradle-plugins.

buildscript {
	repositories {
		mavenLocal()
		mavenRepo name: 'trifork-releases', urls: 'http://nexus.ci81.trifork.com/content/groups/public/'
		mavenRepo name: 'trifork-snapshots', urls: 'http://nexus.ci81.trifork.com/content/groups/public-snapshots/'
		
	}
	dependencies {
		classpath 'com.trifork:gradle-plugins:0.1.0'
	}
}
